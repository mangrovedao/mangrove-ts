// SPDX-License-Identifier:	BSD-2-Clause

// PriceFed.sol

// Copyright (c) 2021 Giry SAS. All rights reserved.

// Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

// 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
pragma solidity ^0.8.10;
pragma abicoder v2;

import "contracts/toy_strategies/single_user/abstract/Defensive.sol";
import "contracts/toy_strategies/single_user/abstract/AaveV3Lender.sol";

contract PriceFed is Defensive, AaveV3Lender {
  constructor(
    address _oracle,
    address _addressesProvider,
    IMangrove _MGV,
    address deployer
  )
    Defensive(_oracle)
    AaveV3Module(_addressesProvider, 0, 0)
    MangroveOffer(_MGV)
  {
    set_gasreq(800_000);
    if (deployer != msg.sender) {
      setAdmin(deployer);
    }
  }

  event Slippage(
    IMangrove mangrove,
    address indexed outbound_tkn,
    address indexed inbound_tkn,
    uint indexed offerId,
    uint old_wants,
    uint new_wants
  );

  function __posthookFallback__(
    ML.SingleOrder calldata order,
    ML.OrderResult calldata result
  ) internal override returns (bool) {
    // reposts only if offer was reneged due to a price slippage
    if (result.makerData == "mgvOffer/abort/reneged") {
      uint price_quote = oracle.getPrice(order.inbound_tkn);
      uint price_base = oracle.getPrice(order.outbound_tkn);

      uint new_offer_wants = (order.offer.gives() * price_base) / price_quote;
      emit Slippage(
        MGV,
        order.outbound_tkn,
        order.inbound_tkn,
        order.offerId,
        order.offer.wants(),
        new_offer_wants
      );
      // since offer is persistent it will auto refill if contract does not have enough provision on the Mangrove
      try
        MGV.updateOffer(
          order.outbound_tkn,
          order.inbound_tkn,
          new_offer_wants,
          order.offer.gives(),
          ofr_gasreq(),
          0,
          0,
          order.offerId
        )
      {
        return true;
      } catch {
        return false;
      }
    } else {
      return super.__posthookFallback__(order, result);
    }
  }

  // Closing diamond inheritance for solidity compiler
  // get/put and lender strat's functions
  function __get__(uint amount, ML.SingleOrder calldata order)
    internal
    override(SingleUser, AaveV3Lender)
    returns (uint)
  {
    return AaveV3Lender.__get__(amount, order);
  }

  function __put__(uint amount, ML.SingleOrder calldata order)
    internal
    override(SingleUser, AaveV3Lender)
    returns (uint)
  {
    return AaveV3Lender.__put__(amount, order);
  }

  // lastlook is defensive strat's function
  function __lastLook__(ML.SingleOrder calldata order)
    internal
    virtual
    override(MangroveOffer, Defensive)
    returns (bool)
  {
    return Defensive.__lastLook__(order);
  }
}
